---
title: Tag Syntax
order: 2
---

# Синтаксис для тегов

## Создание экземпляра тегов

```imba
let element = <div.main> "Hello"
```

Вышеуказанное объявление может показаться странным на первый взгляд. В Imba элементы DOM являются полноценными объектами. Мы создаем *реальный* DOM-элемент с классом "main" и текстом "Hello".

Давайте разберем основной синтаксис, прежде чем перейти к более продвинутым примерам. Поскольку установка классов элементов очень распространена, Imba имеет специальный сокращенный синтаксис для этого. Определение классов осуществляется путем добавления .classname после типа тега. Вы можете добавить несколько классов, например <div.large.header.primary>. Можете ли вы догадаться, как установить идентификатор тега? Здесь используется похожий синтаксис: <div#app.large.ready>.

## Условные классы 

Одна очень распространенная потребность при разработке веб-приложений - установка className только в случае выполнения определенного условия. В Imba также есть удобный синтаксис для этого:

```imba
# only add 'ready' class if expression is truthy
<div.header .ready=expression>
```


## Динамические классы

Если вам нужно установить полностью динамические классы, например, когда состояние представляет собой переменную со строковым значением, вы можете сделать это следующим образом:

```imba
let state = "busy"
<div.header .{state}>
```


## Установка встроенных стилей

```imba
<div css:display='block' css:color='red'>
```


## Установка пользовательских данных

Когда мы перейдем к пользовательским тегам, вы обнаружите, что теги очень часто представляют какие-то данные.

```imba
<AppView[myData] title="Application">
```


## Рендеринг списков


Вы могли заметить, что мы никогда не закрываем наши теги. Вместо использования фигурных скобок или ключевых слов "начало/конец", блоки и теги ограничиваются отступами. Это может показаться странным в начале, но такой подход делает код очень читаемым и лаконичным. Поэтому, если мы хотим создать список с некоторыми дочерними элементами, мы просто пишем:


```imba
<ul>
    <li> "Understand indentation"
    <li> "Get used to it"
    <li> "Cherish it"
```

Если у нас есть динамический список, мы можем использовать простой цикл `for in`:

```imba
<ul> for activity in ["Eat", "Sleep", "Code"]
    <li> <span.name> activity
```

## Условный рендеринг

```imba
<div>
    if isLoggedIn
        <a href="/logout"> "Log out"
    else
        <a href="/register"> "Register"
```

## Реактивный рендеринг

Когда мы объясняем пользовательские теги, вы узнаете, что все, что находится внутри `<self>`, по умолчанию является реактивным. Завершение тега с использованием `->` или `=>` вместо `>` помечает его как реактивный и позволяет вызывать render на теге для повторного рендеринга содержимого.


```imba
var number = 0

var dead = Imba.mount <div>
    <span> "Dead time is {Date.new.toLocaleString}"
    <span> "Number is {number}"

var live = Imba.mount <div ->
    <span> "Live time is {Date.new.toLocaleString}"
    <span> "Number is {number}"

setInterval(&,1000) do
    number++
    dead.render # ничего не меняется
    live.render # содержимое обновляется
```

## Отрисовка в документе

Чтобы добавить теги в фактический документ, следует использовать Imba.mount(element, into). Если вы не указываете второй аргумент, то тег будет добавлен в document.body по умолчанию.


```
Imba.mount <div -> <span> "Let's get started!"
```

